apply plugin: 'com.android.application'

android {
    // Sign APK for Google Play, reading from a directory outside this repository
    // @see http://stackoverflow.com/a/20573171
    afterEvaluate { project ->
        def propsFile = rootProject.file('../KEYTOOLS/keystore.properties')
        def configName = 'release'

        if (propsFile.exists() && android.signingConfigs.hasProperty(configName)) {
            def props = new Properties()
            props.load(new FileInputStream(propsFile))
            android.signingConfigs[configName].storeFile = file('../KEYTOOLS/' + props['storeFile'])
            android.signingConfigs[configName].storePassword = props['storePassword']
            android.signingConfigs[configName].keyAlias = props['keyAlias']
            android.signingConfigs[configName].keyPassword = props['keyPassword']
        }
    }
    // Change apk name
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    "MusicStop-${variant.versionName}-${variant.buildType.name }.apk"
            )
        }
    }

    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "com.blackcrowsteam.musicstop"
        minSdkVersion 15 // ICS 4.0.3
        targetSdkVersion 23 // Marshmallow
        // 11 - 1.0.0 Picker by TextField
        // 12 - 1.0.1 Picker by android-numberpicker
        // 13 - 1.0.2 Bugfix ?
        // 14 - 1.0.3 Mute, About url, SingleTaskActivity
        // 15 - 1.0.4 ActionBar Support + German
        // 16 - 1.0.5 Add Wavelock and KitKat Support
        // 17 - 2.0.0 Full Rewrite for Lollipop (
        versionCode 17
        versionName "2.0"
    }
    signingConfigs {
        release {

        }
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.code-troopers.betterpickers:library:2.1.0' // Api 9+
    compile 'com.android.support:design:23.1.0'
    compile 'com.bettervectordrawable:lib:0.5' // 14+

}
